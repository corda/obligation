apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion 4
    minimumPlatformVersion 4
    contract {
        name "Obligation - Worklows"
        vendor "R3"
        licence "Apache 2"
        versionId 1
    }
    signing {
        enabled false
    }
    sealing {
        enabled false
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    // Kotlin.
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Corda dependencies.
    cordaCompile("$corda_release_group:corda-core:$corda_release_version") {
        changing = true
    }

    // Contracts, states and workflow API dependencies.
    cordaCompile project(":contracts")
    cordaCompile project(":workflows-api")

    // Confidential Identities workflows.
    cordaCompile "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"

    // Logging.
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"

    // Testing.
    testCompile project(":oracle")
    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"
}

jar {
    baseName = "obligation-workflows"
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}